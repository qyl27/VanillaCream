plugins {
    id 'me.shedaniel.unified-publishing'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        data {
            data()

            runDir 'run/data'

            programArgs '--all', '--mod', "${project.mod_id}"
            programArgs '--output', file('../src/generated/resources').absolutePath
            programArgs '--existing', file('../src/main/resources').absolutePath
        }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    modApi group: 'games.moegirl.sinocraft', name: 'sinocore-neoforge', version: rootProject.sinocore_version

    common(project(path: ':', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':', configuration: 'transformProductionNeoForge')

    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-neoforge:${rootProject.rei_version}"
}

remapJar {
    // Remap aw to at.
    atAccessWideners.add(loom.accessWidenerPath.get().asFile.name)
}

unifiedPublishing {
    var release = 'true'.equalsIgnoreCase(System.getenv('MOD_RELEASE'))
    if (!release) {
        logger.lifecycle("Skipping publishing release as MOD_RELEASE is not enabled")
        return
    }

    var cfToken = System.getenv('MOD_CURSEFORGE_TOKEN')
    var mrToken = System.getenv('MOD_MODRINTH_TOKEN')
    if (!cfToken) {
        logger.lifecycle("Skipping publishing release as CF token missing")
        return
    }
    if (!mrToken) {
        logger.lifecycle("Skipping publishing release as MR token missing")
        return
    }

    project {
        displayName = "${parent.mod_name} NeoForge ${project.parent.mod_version} for MC ${rootProject.minecraft_version}"
        version = "${project.version}+neoforge"
        gameVersions = [rootProject.minecraft_version]
        gameLoaders = ['neoforge']
        releaseType = 'release'

        mainPublication tasks.remapJar

        relations {
            depends {
                curseforge = 'sinocore'
                modrinth = 'sinocore'
            }
        }

        curseforge {
            token = cfToken
            id = rootProject.curseforge_project_id
        }

        modrinth {
            token = mrToken
            id = rootProject.modrinth_project_id
        }
    }
}
