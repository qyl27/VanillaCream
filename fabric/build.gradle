plugins {
    id 'me.shedaniel.unified-publishing'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modApi group: 'games.moegirl.sinocraft', name: 'sinocore-fabric', version: rootProject.sinocore_version


    common(project(path: ':', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':', configuration: 'transformProductionFabric')

    modRuntimeOnly "com.terraformersmc:modmenu:${rootProject.fabric_mod_menu_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}"
    modRuntimeOnly "vazkii.patchouli:Patchouli:${rootProject.patchouli_version}-FABRIC"
}

remapJar {
    injectAccessWidener = true
}

unifiedPublishing {
    var release = 'true'.equalsIgnoreCase(System.getenv('MOD_RELEASE'))
    if (!release) {
        logger.lifecycle("Skipping publishing release as MOD_RELEASE is not enabled")
        return
    }

    var cfToken = System.getenv('MOD_CURSEFORGE_TOKEN')
    var mrToken = System.getenv('MOD_MODRINTH_TOKEN')
    if (!cfToken) {
        logger.lifecycle("Skipping publishing release as CF token missing")
        return
    }
    if (!mrToken) {
        logger.lifecycle("Skipping publishing release as MR token missing")
        return
    }

    project {
        displayName = "${parent.mod_name} Fabric ${project.parent.mod_version} for MC ${rootProject.minecraft_version}"
        version = "${project.version}+fabric"
        gameVersions = [rootProject.minecraft_version]
        gameLoaders = ['fabric']
        releaseType = 'release'

        mainPublication tasks.remapJar

        relations {
            depends {
                curseforge = 'fabric-api'
                modrinth = 'fabric-api'
            }

            depends {
                curseforge = 'sinocore'
                modrinth = 'sinocore'
            }
        }

        curseforge {
            token = cfToken
            id = rootProject.curseforge_project_id
        }

        modrinth {
            token = mrToken
            id = rootProject.modrinth_project_id
        }
    }
}
